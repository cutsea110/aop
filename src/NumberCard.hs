{-# LANGUAGE NPlusKPatterns #-}
module NumberCard where

import Data.Monoid (Sum(..))

------------------------------------------------------
-- | 2024年度 豊島岡女子中学校 第一回入試問題 第4問
------------------------------------------------------
-- | 3種類のカード [1], [2], [13] がそれぞれたくさんあります。
--   これらのカードを [2] のカードが連続しないように並べて整数を作ります。
--   例えば,
--   1桁の整数は [1], [2] の 2 通り。
--   2桁の整数は [1][1], [1][2], [2][1], [13] の 4 通り。
--   3桁の整数は [1][1][1], [1][1][2], [1][2][1], [1][13], [2][1][1], [2][1][2],
--   [2][13], [13][1], [13][2]の 9 通り作ることができます。
--   このとき, 次の各問に答えなさい。
--
--   (1) 4 桁の整数は何通り作ることができますか。
--   (2) 6 桁の整数は何通り作ることができますか。


gdigits :: Monoid a =>
           (a, (a, a, a))
        -> (a, (a, a, a))
        -> (a -> a)
        -> (a -> a)
        -> (a -> a)
        -> Int -> (a, (a, a, a))
gdigits q0 q1 f1 f2 f13 = u
  where
    u = (map f [0..] !!)
    f 0 = q0
    f 1 = q1
    f n = (p1'<>p2'<>p13', (p1', p2', p13'))
      where
        (t1, (p1, _, p13)) = u (n-1)
        (t2, _)            = u (n-2)
        p1'  = f1  $! t1
        p2'  = f2  $! p1<>p13
        p13' = f13 $! t2

-- | 3要素のリスト
--  1. カード [1]  はじまりの digit 文字列
--  2. カード [2]  はじまりの digit 文字列
--  3. カード [13] はじまりの digit 文字列
digits :: Int -> ([] String, ([] String, [] String, [] String))
digits = gdigits ([""], ([], [], [])) (["1", "2"], (["1"], ["2"], [])) (map ("1"<>)) (map ("2"<>)) (map ("13"<>))

digitCount :: Int -> (Sum Integer, (Sum Integer, Sum Integer, Sum Integer))
digitCount = gdigits (Sum 1, (Sum 0, Sum 0, Sum 0)) (Sum 2, (Sum 1, Sum 1, Sum 0)) id id id
